apply plugin: 'com.android.library'
apply plugin: "me.tatarka.retrolambda"
apply plugin: "com.noveogroup.android.check"

description = 'Easy SharedPreferences API with RxJava2 Integration'

android {
    compileSdkVersion sdkVersions.compileSdk
    buildToolsVersion sdkVersions.buildTools

    defaultConfig {
        minSdkVersion sdkVersions.minSdk
        targetSdkVersion sdkVersions.targetSdk
        versionCode buildVersions.code
        versionName buildVersions.name
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        disable "AllowBackup"
        disable "ContentDescription"
        disable "InvalidPackage"
        disable "SpUsage"
        disable "IconMissingDensityFolder"
        disable "SelectableText"
        disable "RtlCompat", "RtlEnabled", "RtlHardcoded", "RtlSymmetry"
        disable "MissingPrefix"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    /* test dependencies */
    testImplementation libraries.junit
    testImplementation libraries.mockitoCore

    /* Support */
    implementation libraries.supportAnnotations

    /* Utilities */
    implementation libraries.streamSupport

    /* Rx */
    implementation libraries.rxJava2
    implementation libraries.slf4j

}

check {
    abortOnError true
    checkstyle { config project.rootProject.file('config/checkstyle.xml').absolutePath }
    pmd { config project.rootProject.file('config/pmd.xml').absolutePath }
    findbugs { skip true }
}

def publishProperties = new Properties()
publishProperties.load(new FileInputStream(rootProject.file("publish.properties")))

ext {
    artifactId = publishProperties.getProperty("artifact")
    groupId = publishProperties.getProperty("group")
    version = buildVersions.name
    name = publishProperties.getProperty("name")
    desc = publishProperties.getProperty("desc")
    website = publishProperties.getProperty("github")
    pom = {
        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        scm {
            url publishProperties.getProperty("github")
            connection publishProperties.getProperty("scm")
            developerConnection publishProperties.getProperty("scm")
        }
        issueManagement {
            system 'GitHub'
            url publishProperties.getProperty('github') + '/issues'
        }
        developers {
            developer {
                name 'Alexandr Vaytsekhovskiy'
                url 'https://github.com/avaytsekhovskiy'
                roles {
                    role 'architect'
                    role 'developer'
                }
            }
            developer {
                name 'Sergey Malichenko'
                url 'https://github.com/smalichenko'
                roles {
                    role 'developer'
                }
            }
        }
    }
    bintray = {
        repo = publishProperties.getProperty("bintrayRepo")
        name = publishProperties.getProperty("artifact")
        licenses = ['MIT']
        vcsUrl = publishProperties.getProperty("github")
        issueTrackerUrl = publishProperties.getProperty("github") + "/issues"
        websiteUrl = publishProperties.getProperty("github")
        publish = true
        publicDownloadNumbers = true
        version {
            name = buildVersions.name
            vcsTag = buildVersions.name
        }
    }
}

project.archivesBaseName = 'preferenceentity' // to fix that project name different from artifact name
apply from: 'androidmaven.gradle'
apply from: 'bintray.gradle'