apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

def publishProperties = new Properties()
publishProperties.load(new FileInputStream(rootProject.file("publish.properties")))

group = groupId // Maven Group ID for the artifact
version = buildVersions.name


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

//javadoc classpath with library dependencies
afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompiler.classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId groupId
            artifactId artifactId
            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                url publishProperties.getProperty("github")
                connection publishProperties.getProperty("scm")
                developerConnection publishProperties.getProperty("scm")
            }
            issueManagement {
                system 'GitHub'
                url publishProperties.getProperty('github') + '/issues'
            }
            developers {
                developer {
                    name 'Alexandr Vaytsekhovskiy'
                    url 'https://github.com/avaytsekhovskiy'
                    roles {
                        role 'architect'
                        role 'developer'
                    }
                }
                developer {
                    name 'Sergey Malichenko'
                    url 'https://github.com/smalichenko'
                    roles {
                        role 'developer'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/preferenceentity-release.aar")
            groupId groupId
            artifactId artifactId
            version buildVersions.name

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}